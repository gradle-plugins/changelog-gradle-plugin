buildscript {
  repositories {
    mavenLocal()
    jcenter()
  }
}

plugins {
  id "com.jfrog.bintray" version "1.2"
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'
apply plugin: 'maven'

sourceCompatibility = '1.8'
targetCompatibility = '1.7'

group = project.group
version = project.version

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://repo.gradle.org/gradle/libs" }
}

dependencies {
  compile 'commons-configuration:commons-configuration:1.10'
  compile gradleApi()
  compile localGroovy()
  testCompile "org.spockframework:spock-maven:$spockVersion",
      "junit:junit:$junitVersion"
}

task wrapper(type: Wrapper) { gradleVersion = 2.1  }

ext {
  project_website = "https://github.com/{$orgId}/${projectId}"
  project_scm = "scm:git@github.com:${orgId}/${projectId}.git"
  license_url = "https://raw.github.com/${orgId}/${projectId}/master/license.txt"
}

if( !hasProperty( 'bintrayUser' ) )
  ext.bintrayUser = ''

if( !hasProperty( 'bintrayKey' ) )
  ext.bintrayKey = ''

def pomConfig = {
  licenses {
    license {
      name "The Apache Software License, Version 2.0"
      url "http://www.apache.org/licenses/LICENSE-2.0.txt"
      distribution "repo"
    }
  }
  developers {
    developer {
      id "christian-soth"
      name "Christian Soth"
      email "christian.soth@devbliss.com"
    }
    developer {
      id "philipp-karstedt"
      name "Philipp Karstedt"
      email "philipp.karstedt@devbliss.com"
    }
    developer {
      id "henning-schuetz"
      name "Henning Sch√ºtz"
      email "henning.schuetz@devbliss.com"
    }
  }
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

bintray {
  user = bintrayUser
  key = bintrayKey

  configurations = ['archives']

  dryRun = false //Whether to run this as dry-run, without deploying
  publish = false //If version should be auto published after an upload
  pkg {
    repo = 'gradle-plugin-repository'
    userOrg = orgId
    name = 'changelog-gradle-plugin'
    desc = 'Gradle changelog plugin. A plugin for the gradle build system to write changelogs painlessly.'
    websiteUrl = project.project_website
    issueTrackerUrl = "https://github.com/{$orgId}/${projectId}/issues"
    vcsUrl = project.project_scm
    licenses = ['Apache-2.0']
    labels = [
      'changelog',
      'gradle',
      'plugin',
      'snapshot',
      'release',
      'releasenote'
    ]
    publicDownloadNumbers = true
    version {
      name = project.version
      userOrg = orgId
      vcsUrl = "https://github.com//{$orgId}/${projectId}.git"
      vcsTag = project.version
      attributes = ['gradle-plugin': 'org.gradliss.changelog:org.gradliss.changelog:changelog-gradle-plugin']
    }
  }
}
